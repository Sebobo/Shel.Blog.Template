prototype(Neos.Neos:Page) {
    head {
        viewport = '<meta name="viewport" content="width=device-width, initial-scale=1, user-scalable=no"/>'
        stylesheets {
            main = afx`
                <link rel="stylesheet" @children="attributes.href">
                    <Neos.Fusion:ResourceUri path="resource://Shel.Blog.Template/Public/Styles/main.css"/>
                </link>
            `
        }
    }

    bodyTag {
        attributes.class = 'is-preload'
    }

    body {
        templatePath = 'resource://Shel.Blog.Template/Private/Templates/Page/Default.html'

        content = Neos.Fusion:Component {
            styles = Neos.Fusion:DataStructure {
                # Split theme color selected in backend into its components to generate color variations in css
                primaryThemeColorComponents = ${String.pregSplit(q(site).property('primaryThemeColor'), '/,/')}
                @ignoreProperties = ${['primaryThemeColorComponents']}

                '--primary-theme-color-h' = ${String.toFloat(String.replace(this.primaryThemeColorComponents[0], 'hsla(', ''))}
                '--primary-theme-color-s' = ${String.toFloat(this.primaryThemeColorComponents[1]) + '%'}
                '--primary-theme-color-l' = ${String.toFloat(this.primaryThemeColorComponents[2]) + '%'}
                '--primary-theme-color-a' = ${String.toFloat(this.primaryThemeColorComponents[3])}

                @process.concat = Neos.Fusion:Reduce {
                    items = ${value}
                    itemName = 'item'
                    itemKey = 'itemKey'
                    initialValue = ''
                    itemReducer = ${carry + itemKey + ':' + item + ';'}
                }
                @if.hasThemeColor = ${q(site).property('primaryThemeColor')}
            }

            main = Neos.Fusion:Join

            sidebar = Neos.Fusion:Join {
                menu = Shel.Blog.Template:Component.Menu {
                    @position = 'start'
                }

                contact = Neos.Fusion:Component {
                    email = ${q(site).property('email')}
                    phone = ${q(site).property('phone')}
                    address = ${q(site).property('address')}
                    city = ${q(site).property('city')}

                    renderer = afx`
                        <section>
                            <header class="major">
                                <h2>Get in touch</h2>
                            </header>
                            <p>
                                Sed varius enim lorem ullamcorper dolore aliquam aenean ornare velit lacus,
                                ac varius enim lorem ullamcorper dolore. Proin sed aliquam facilisis ante interdum.
                                Sed nulla amet lorem feugiat tempus aliquam.
                            </p>
                            <ul class="contact">
                                <li class="icon solid fa-envelope"><a href={'mailto:' + props.email}>{props.email || 'not set'}</a></li>
                                <li class="icon solid fa-phone">{props.phone || 'not set'}</li>
                                <li class="icon solid fa-home">{props.address || 'not set'}<br />{props.city || 'not set'}</li>
                            </ul>
                        </section>
                    `
                }

                footer = Neos.Fusion:Component {
                    @position = 'end'
                    renderer = afx`
                        <footer id="footer">
                            {props.footer}
                            <p class="copyright">
                                &copy; Untitled. All rights reserved.<br/>
                                Design: <a href="https://html5up.net">HTML5 UP</a>.<br/>
                                Neos CMS adaption: <a href="https://helzle.it">Sebastian Helzle</a>.
                            </p>
                        </footer>
                    `
                }
            }

            renderer = afx`
                <div id="wrapper" style={props.styles} style.@if.hasStyles={props.styles}>
                    <div id="main">
                        <div class="inner">
                            <Shel.Blog.Template:Component.Header/>
                            {props.main}
                        </div>
                    </div>
                    <div id="sidebar">
                        <div class="inner">
                            {props.sidebar}
                        </div>
                    </div>
                </div>
            `
        }

        javascripts {
            jQuery = afx`<script><Neos.Fusion:ResourceUri @path="attributes.src" path="resource://Shel.Blog.Template/Public/JavaScript/jquery.min.js"/></script>`
            browser = afx`<script><Neos.Fusion:ResourceUri @path="attributes.src" path="resource://Shel.Blog.Template/Public/JavaScript/browser.min.js"/></script>`
            breakpoints = afx`<script><Neos.Fusion:ResourceUri @path="attributes.src" path="resource://Shel.Blog.Template/Public/JavaScript/breakpoints.min.js"/></script>`
//            util = afx`<script><Neos.Fusion:ResourceUri @path="attributes.src" path="resource://Shel.Blog.Template/Public/JavaScript/util.js"/></script>`
            main = afx`<script><Neos.Fusion:ResourceUri @path="attributes.src" path="resource://Shel.Blog.Template/Public/JavaScript/main.js"/></script>`
        }
    }
}
