prototype(Shel.Blog.Template:Component.Contact) < prototype(Neos.Neos:ContentComponent) {
    @context.node = ${site}
    email = ${q(node).property('email')}
    phone = ${q(node).property('phone')}
    address = ${q(node).property('address')}
    city = ${q(node).property('city')}

    contactIntroduction = Neos.Neos:Editable {
        property = 'contactIntroduction'
        block = false
    }

    renderer = afx`
        <section>
            <header class="major">
                <h2>Get in touch</h2>
            </header>
            <p>{props.contactIntroduction}</p>
            <ul class="contact">
                <li class="icon solid fa-envelope" @if.set={props.email}>
                    <a href={'mailto:' + props.email}>{props.email}</a>
                </li>
                <li class="icon solid fa-phone" @if.set={props.phone}>
                    {props.phone}
                </li>
                <li class="icon solid fa-home" @if.set={props.address && props.city}>
                    {props.address}<br />{props.city}
                </li>
            </ul>
        </section>
    `

    @cache {
        mode = 'cached'
        entryIdentifier {
            node = ${node}
            editPreviewMode = ${node.context.currentRenderingMode.name}
        }

        entryTags {
            node = ${Neos.Caching.nodeTag(node)}
        }
    }
}
